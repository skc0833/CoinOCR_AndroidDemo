import java.security.MessageDigest

apply plugin: 'com.android.application'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.skc.coin_ocr"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions -Wno-format"
                arguments '-DANDROID_PLATFORM=android-23', '-DANDROID_STL=c++_shared' ,"-DANDROID_ARM_NEON=TRUE"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    splits {
        abi {
            enable true
            reset()
            //include "arm64-v8a", 'armeabi-v7a'
            include "arm64-v8a"
        }
    }
    // skc fix error: lambda expressions are not supported in -source 1.7
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    def camerax_version = "1.1.0-beta01"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
}

def archives = [
        [
                'src' : 'https://paddleocr.bj.bcebos.com/libs/paddle_lite_libs_v2_10.tar.gz',
                'dest': 'PaddleLite'
        ],
        [
                'src' : 'https://paddlelite-demo.bj.bcebos.com/libs/android/opencv-4.2.0-android-sdk.tar.gz',
                'dest': 'OpenCV'
        ]
//        [
//                'src' : 'https://paddleocr.bj.bcebos.com/PP-OCRv2/lite/ch_PP-OCRv2.tar.gz',
//                'dest' : 'src/main/assets/models'
//        ],
//        [
//                'src' : 'https://paddleocr.bj.bcebos.com/dygraph_v2.0/lite/ch_dict.tar.gz',
//                'dest' : 'src/main/assets/labels'
//        ]
]

task downloadAndExtractArchives(type: DefaultTask) {
    doFirst {
        println "Downloading and extracting archives including libs and models"
    }
    doLast {
        // Prepare cache folder for archives
        String cachePath = "cache"
        if (!file("${cachePath}").exists()) {
            mkdir "${cachePath}"
        }
        archives.eachWithIndex { archive, index ->
            MessageDigest messageDigest = MessageDigest.getInstance('MD5')
            messageDigest.update(archive.src.bytes)
            String cacheName = new BigInteger(1, messageDigest.digest()).toString(32)
            // Download the target archive if not exists
            boolean copyFiles = !file("${archive.dest}").exists()
            if (!file("${cachePath}/${cacheName}.tar.gz").exists()) {
                ant.get(src: archive.src, dest: file("${cachePath}/${cacheName}.tar.gz"))
                copyFiles = true // force to copy files from the latest archive files
            }
            // Extract the target archive if its dest path does not exists
            if (copyFiles) {
                copy {
                    from tarTree("${cachePath}/${cacheName}.tar.gz")
                    into "${archive.dest}"
                }
            }
        }
    }
}
preBuild.dependsOn downloadAndExtractArchives

// skc 공통 모델 파일을 복사해준다(git 용량을 줄이기 위함).
// app/src/main/ 이 아닌 src/main 경로임에 주의!!!
String skc_copy_src_model1 = "src/main/assets/models/common/cls.nb"
String skc_copy_src_model2 = "src/main/assets/models/common/rec_crnn.nb"

// skc_copy_src_model1=D:\research\CoinOCR_AndroidDemo\app\src\main\assets\models\common\cls.nb
println 'skc_copy_src_model1=' + file({skc_copy_src_model1})

tasks.register('copyModel0', Copy) {
    //from "${skc_copy_src_model1}", "${skc_copy_src_model2}"
    from "${skc_copy_src_model1}"
    into 'src/main/assets/models/ch_PP-OCRv2'
}
tasks.register('copyModel1', Copy) {
    from "${skc_copy_src_model1}"
    into 'src/main/assets/models/ch_PP-OCRv3_infer'
}
tasks.register('copyModel2', Copy) {
    from "${skc_copy_src_model1}"
    into 'src/main/assets/models/en_PP-OCRv3_infer'
}
tasks.register('copyModel3', Copy) {
    from "${skc_copy_src_model1}"
    into 'src/main/assets/models/ch_PP-OCR_V3_add_60x2_230708'
}
tasks.register('copyModel4', Copy) {
    from "${skc_copy_src_model1}"
    into 'src/main/assets/models/ch_PP-OCR_V3_500_add_500_noflip_rot30'
}
preBuild.dependsOn copyModel0, copyModel1, copyModel2, copyModel3, copyModel4
